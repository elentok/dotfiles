#!/usr/bin/env fish

function usage
    echo "Usage:"
    echo
    echo "  encrypted-volume create <file.img> <name> <size>"
    echo "  encrypted-volume open <file.img> <name>"
    echo "  encrypted-volume close <file.img> <name>"
    echo
    echo "NOTE: this required installing 'cryptsetup' from apt"
end

if test (count $argv) -lt 3
    usage
    exit 1
end

set filename $argv[2]
set name $argv[3]

switch $argv[1]
    case create
        if test (count $argv) -lt 4
            usage
            exit 1
        end
        set size $argv[4]
        echo "Creating volume $filename of size $size MB..."
        dd if=/dev/urandom of=$filename bs=1M count=$size
        echo "Encrypting $filename..."
        cryptsetup --verify-passphrase luksFormat $filename
        echo "Opening vault $filename..."
        sudo cryptsetup open --type luks $filename $name
        echo "Creating filesystem on $filename..."
        sudo mkfs.ext4 -L $name /dev/mapper/$name
        echo "Done."
    case open
        echo "Opening value $filename..."
        sudo cryptsetup open --type luks $filename $name
        echo "Creating /mnt/$name..."
        sudo mkdir -p /mnt/$name
        echo "Mounting /mnt/$name..."
        sudo mount /dev/mapper/$name /mnt/$name
        echo "Done."
    case close
        echo "Unmounting /mnt/$name..."
        sudo umount /mnt/$name
        echo "Closing vault $name..."
        sudo cryptsetup close $name
        echo "Done."
end
