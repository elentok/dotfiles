#!/usr/bin/env fish
#
# Pdf Decrypt
#
# Usage:
#
#   pdfdecrypt {input} {output}
#
# To avoid entering the password each time:
#
#   set -x PDF_PASS {password}
#

if test (count $argv) -lt 2
    usage (status --current-filename)
    exit 1
end

set pass $PDF_PASS
if test -z "$pass"
    set pass (read -P "Enter password: " -s)
end

if type -q pdftk
    pdftk $argv[1] input_pw "$pass" output $argv[2]
else if type -q qpdf
    qpdf --password="$pass" --decrypt $argv[1] $argv[2]
else
    echo "Please install either pdftk or qpdf"
end
