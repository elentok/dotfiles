#
# A simple theme from elentok.
#
#

function +vi-git-status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{8}✗%f'
  fi
}

function prompt_elentok_precmd {
  vcs_info

  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_elentok_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_elentok_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' get-revision true
  zstyle ':vcs_info:*' use-simple true
  zstyle ':vcs_info:*' unstagedstr ' %F{red}✗%f'
  zstyle ':vcs_info:*' formats ' %F{green}[%b]%f%u'
  #zstyle ':vcs_info:*' formats ' %F{green}[%b]%f %F{white}%.7i%f%u'
  zstyle ':vcs_info:*' actionformats ' %F{blue}%b%f %F{white}%.7i%f +%a%u'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format ' %F{8}(%v)%f'

  # Define prompts.
  PROMPT="
%F{blue}%~%f\${vcs_info_msg_0_}\${ruby_info[version]}
%(?.%F{green}${1:-☻ }%f.%F{red}${1:-☻ }%f)"
#RPROMPT='${ruby_info[version]}'

}

function prompt_elentok_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt elentok [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_elentok_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 'elentok' "$@"
  else
    prompt_preview_theme 'elentok'
    print
    prompt_preview_theme 'elentok' "❯"
    print
    prompt_preview_theme 'elentok' "$"
  fi
}

prompt_elentok_setup "$@"

